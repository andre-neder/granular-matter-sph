#version 460

#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_shader_atomic_float : require

#define UINT_MAX (0xffffffff)
#define FLOAT_MAX 3.402823466e+38
#define EPSILON 0.0000001f
#define PI      3.1415926f
#define BUFFER_A 0
#define BUFFER_B 1

layout (local_size_x_id = 1, local_size_y = 1, local_size_z = 1) in;

struct LRParticle{
    vec2 position;
    vec2 predPosition;
    vec2 velocity;
    vec2 externalForce;
    vec2 internalForce;
    float rho;
    float p;
    float V;
    float a;
    vec2 d;
    mat2 stress;
    vec2 dijpj;
    float dpi;
    float lastP;
    float densityAdv;
    float pad0;
    float pad1;
    float pad2;
};


layout(set = 0, binding = BUFFER_B) buffer StorageInput{
    LRParticle particles[];
} inStorage;

layout(set = 0, binding = BUFFER_A) buffer StorageOutput{
    LRParticle particles[];
} outStorage;

layout(set = 0, binding = 3) uniform sampler volumeMapSampler; 
layout(set = 0, binding = 4) uniform texture2D sdfTexture[]; 
 
layout( push_constant ) uniform Settings{
    vec2 g; 
    float r_LR;                 
    float h_LR; 

    float rho0; 
    float mass;
    float stiffness;	  
    float dt;	 

    float DOMAIN_WIDTH; 
    float DOMAIN_HEIGHT;  
    float sleepingSpeed;
    float h_HR;

    float theta;       
    float sigma;                           
    float alpha;                             
    uint n_HR;                              
    
    float pad2;                                
    float dragCoefficient;                
    float rhoAir;                             
    float pad3;
} settings;

struct ParticleGridEntry{
    uint particleIndex;
    uint cellKey;
};

layout(set = 1, binding = 0) buffer GridLookUpStorage{
    ParticleGridEntry entries[];
} gridLookup;

layout(set = 1, binding = 2) buffer StartringIndicesStorage{
    uint startingIndices[];
};


uint calculateCellKey(uvec2 cell){
    return (cell.x * 3079 + cell.y * 1543) % gridLookup.entries.length();
}

//* https://github.com/glslify/glsl-frobenius/blob/master/index.glsl
float frobenius(mat2 m) {
  return sqrt(dot(m[0],m[0]) + dot(m[1],m[1]));
}

float frobenius(mat3 m) {
  return sqrt(dot(m[0],m[0]) + dot(m[1],m[1]) + dot(m[2],m[2]));
}

float rand(){
    return fract(sin(dot(vec2(settings.dt * 25165843.f, settings.dt * 786433), vec2(12.9898, 78.233))) * 43758.5453);
}

float W(float r, float h){
    float volume = PI * pow(h, 4.f) / 6.f;
    return (h - r) * (h - r) / volume;
}
vec2 gradW(vec2 rij, float h){
    float r = length(rij);
    vec2 dir = r <= EPSILON ? vec2(rand(), rand()) : normalize(rij); 
    float scale = -12.f / (PI * pow(h, 4.f));
    float slope = scale * (r - h);
    return -dir * slope;
}

vec2 gradW(float r, vec2 rij, float h){
    vec2 dir = r <= EPSILON ? vec2(rand(), rand()) : normalize(rij); 
    float scale = -12.f / (PI * pow(h, 4.f));
    float slope = scale * (r - h);
    return -dir * slope;
}

vec2 adjustKernelRadiusOffset(vec2 position){
    return vec2(position.x - 2 * settings.h_LR, position.y - 2 * settings.h_LR);
}
vec2 adjustKernelRadiusScale(vec2 scale){
    return vec2(scale + 4 * settings.h_LR);
}

#define for_all_fluid_neighbors(code) { \
    ivec2 particleCell = ivec2(floor(vec2(p.predPosition / settings.h_LR))); \
    for (int k = -1; k <= 1; k++){ \
        for (int l = -1; l <= 1; l++){ \
            ivec2 cell = ivec2(particleCell.x + k, particleCell.y + l); \
            uint cellKey = calculateCellKey(uvec2(cell)); \
            uint startIndex = startingIndices[cellKey]; \
            for(uint i = startIndex; i < gridLookup.entries.length(); i++) { \
                if(gridLookup.entries[i].cellKey != cellKey){ \
                    break; \
                } \
                uint particleIndex = gridLookup.entries[i].particleIndex; \
                LRParticle pi = inStorage.particles[particleIndex]; \
                vec2 p_pi = p.predPosition - pi.predPosition;\
                float r = length(p_pi); \
                code \
            } \
        }  \
    } \
}



float halfBoxSize = settings.DOMAIN_HEIGHT / 4;
vec2 rigidBodyPositions[] = {
    vec2( 0, 0), // Bottom
    vec2( 0, 0), // Left
    vec2( settings.DOMAIN_WIDTH, 0), // Right
    // vec2( settings.DOMAIN_WIDTH / 2.0 - halfBoxSize, 0),
};
vec2 rigidBodyScale[] = {
    vec2(1), vec2(1), vec2(1), 
    // vec2(halfBoxSize * 2),
};

void main(){

    uint particleID = gl_GlobalInvocationID.x;;
    LRParticle p = inStorage.particles[particleID];

    float sum = 0.0;

    for_all_fluid_neighbors(
        if (r < settings.h_LR)
        {
            vec2 dji = p.dpi * gradW(p_pi, settings.h_LR);
            vec2 dji_pi = dji * p.lastP;
            sum += pi.V * dot(p.dijpj - pi.d * pi.lastP - (pi.dijpj - dji_pi), gradW(p_pi, settings.h_LR));
        }
    )

    // Volume maps
    for (int i = 0; i < rigidBodyPositions.length(); i++){ 
        vec2 samplePosition = (p.predPosition - adjustKernelRadiusOffset(rigidBodyPositions[i])) / adjustKernelRadiusScale(rigidBodyScale[i]);
        vec2 p_pi = texture(sampler2D(sdfTexture[i], volumeMapSampler), samplePosition).gb;
        float volume = texture(sampler2D(sdfTexture[i], volumeMapSampler), samplePosition).r;
        float r = length(p_pi);
        if(r < settings.h_LR){
            sum += volume * dot(p.dijpj, gradW(p_pi, settings.h_LR));
        }
    }
    float b = 1.0 - p.densityAdv;
    float dtSq = settings.dt * settings.dt;
    float omega = 0.5;
    float denom = p.a * dtSq;
    p.p = 0.0;
    if(abs(denom) > EPSILON){
        p.p = max((1.0 - omega) * p.lastP + omega / denom * (b - dtSq * sum), 0.0);
    }

    // if(p.p != 0.0){
    //     float newDensity = settings.rho0 * ((p.a * p.p + sum) * dtSq - b) + settings.rho0;

    //     atomicAdd(averageDensityError, newDensity - settings.rho0); //? just do 2 iterations for now
    // }

    outStorage.particles[particleID] = p;
}

