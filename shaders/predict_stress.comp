#version 460
#define VULKAN 100
#define EPSILON 0.0000001f
#define PI      3.1415926f
#define BUFFER_A 0
#define BUFFER_B 1

layout (local_size_x_id = 1, local_size_y = 1, local_size_z = 1) in;

struct Particle{
    vec2 position;
    vec2 predPosition;
    vec2 velocity;
    vec2 predVelocity;
    vec2 pressureForce;
    float rho;
    float p;
    float V;
    float boundaryVolume;
    vec2 boundaryNormal;
    mat2 stress;
    uint fluidNeighbors[31];
    uint fluidNeighborCount;
};


layout(set = 0, binding = BUFFER_A) buffer StorageInput{
    Particle particles[];
} inStorage;

layout(set = 0, binding = BUFFER_B) buffer StorageOutput{
    Particle particles[];
} outStorage;

layout(set = 0, binding = 3) buffer BoundaryStorage{
    Particle particles[];
} boundary;

layout( push_constant ) uniform Settings{
    vec2 g; 
    float particleRadius;                 
    float kernelRadius; 

    float rho0; 
    float mass;
    float stiffness;	  
    float dt;	 

    float DOMAIN_WIDTH; 
    float DOMAIN_HEIGHT;  
    float pad1;
    float pad2;

    float theta;       
    float sigma;                           
    float alpha;                             
    float beta;                              
    
    float C;                                
    float dragCoefficient;                
    float rhoAir;                             
    float pad3;
} settings;

struct ParticleGridEntry{
    uint particleIndex;
    uint cellKey;
};

layout(set = 1, binding = 0) buffer GridLookUpStorage{
    ParticleGridEntry entries[];
} gridLookup;

layout(set = 1, binding = 2) buffer StartringIndicesStorage{
    uint startingIndices[];
};

uint calculateCellKey(uvec2 cell){
    return (cell.x * 3079 + cell.y * 1543) % gridLookup.entries.length();
}

//* https://github.com/glslify/glsl-frobenius/blob/master/index.glsl
float frobenius(mat2 m) {
  return sqrt(dot(m[0],m[0]) + dot(m[1],m[1]));
}

float frobenius(mat3 m) {
  return sqrt(dot(m[0],m[0]) + dot(m[1],m[1]) + dot(m[2],m[2]));
}

float rand(){
    return fract(sin(dot(vec2(settings.dt * 25165843.f, settings.dt * 786433), vec2(12.9898, 78.233))) * 43758.5453);
}

float W(float r, float h){
    float volume = PI * pow(h, 4.f) / 6.f;
    return (h - r) * (h - r) / volume;
}
vec2 gradW(vec2 rij, float h){
    float r = length(rij);
    vec2 dir = r <= EPSILON ? vec2(rand(), rand()) : normalize(rij); 
    float scale = -12.f / (PI * pow(h, 4.f));
    float slope = scale * (r - h);
    return -dir * slope;
}

vec2 gradW(float r, vec2 rij, float h){
    vec2 dir = r <= EPSILON ? vec2(rand(), rand()) : normalize(rij); 
    float scale = -12.f / (PI * pow(h, 4.f));
    float slope = scale * (r - h);
    return -dir * slope;
}

void main(){
    uint particleID = gl_GlobalInvocationID.x;;
    Particle p = inStorage.particles[particleID];

    //* Strain & Stress 
    mat2 velocityGradient = mat2(0.f);
    mat2 D = mat2(0.f);

    // For all previously stored fluid neighbors
    for(uint i = 0; i < p.fluidNeighborCount; i++)
    {
        uint particleIndex = p.fluidNeighbors[i];
        Particle pi = inStorage.particles[particleIndex];

        
        vec2 p_pi = p.predPosition - pi.predPosition;
        float r = length(p_pi);
    
        if (r < settings.kernelRadius)
        {
            //* Strain & Stress 
            vec2 gradient = gradW(p_pi, settings.kernelRadius);
            velocityGradient += pi.V * outerProduct(gradient, p.predVelocity - pi.predVelocity);
            D += (1.f / pi.rho) * outerProduct(gradient, gradient);
        }
    }

    //* For all boundary particles
    // for (int i = 0; i < p.boundaryNeighborCount; i++)
    // {
    //     uint particleIndex = p.boundaryNeighbors[i];
    //     Particle pb = boundary.particles[particleIndex];
    //     vec2 p_pb = p.predPosition - pb.position;
    //     float r = length(p_pb);
       
    //     if (r < settings.kernelRadius)
    //     {
    //         //* Strain & Stress 
    //         vec2 gradient = gradW(p_pb, settings.kernelRadius);
    //         velocityGradient += pb.V * outerProduct(gradient, p.predVelocity - pb.predVelocity);
    //         D += (1.f / pb.rho) * outerProduct(gradient, gradient);
    //     }
    // }

    {
        vec2 gradient = gradW(p.boundaryNormal, settings.kernelRadius);
        velocityGradient += p.boundaryVolume * outerProduct(gradient, p.predVelocity - vec2(0,0));
        D += (1.f / settings.rho0) * outerProduct(gradient, gradient);
    }

    //* https://dl.acm.org/doi/pdf/10.1145/2019406.2019410
    //? http://gamma.cs.unc.edu/granular/narain-2010-granular.pdf
    D *= (2.f * settings.mass * settings.mass * settings.dt) / (p.rho * p.rho);
    mat2 strainTensor = 0.5f * (velocityGradient + transpose(velocityGradient));
    mat2 stressTensor = inverse(D) * strainTensor;
    mat2 meanHydrostaticStressTensor = 0.5f * (stressTensor[0][0] + stressTensor[1][1]) * mat2(1.f);
    mat2 deviatoricStressTensor = stressTensor - meanHydrostaticStressTensor;
    
    float alpha = sqrt(2.f / 3.f) * sin(settings.theta); //* frictional coefficient
    float yield = sqrt(3.f) * alpha * p.p; //* Drucker-Prager yield criterion

    mat2 stress = mat2(0);
    mat2 cohesion = mat2(0);

    float normDeviatoricStressTensor = frobenius(deviatoricStressTensor);
    if(p.p > EPSILON && normDeviatoricStressTensor > EPSILON){
        stress = normDeviatoricStressTensor <= yield ? deviatoricStressTensor : deviatoricStressTensor * (yield / normDeviatoricStressTensor);
    }

    p.stress = stress;
    
    // float maxCohesion = settings.beta * settings.beta * settings.C;
    // float normStressTensor = frobenius(stressTensor);
    // if(p.p > EPSILON && normStressTensor > EPSILON){
    //     cohesion = normStressTensor <= maxCohesion ? stressTensor : stressTensor * (maxCohesion / normStressTensor);
    // }
    // p.pad0 = normStressTensor;
    // p.stress = frobenius(stress) > frobenius(cohesion) ? stress : cohesion;



    outStorage.particles[particleID] = p;
}

// volume = mass / density